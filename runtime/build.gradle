buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.6.1'
    }
}

apply plugin: 'java'
apply plugin: 'me.tatarka.retrolambda'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    compile project(":annotations")
}

retrolambda {
    defaultMethods true
}

if (rootProject.hasProperty('versionName') && rootProject.hasProperty('bintrayUser')) {
    apply plugin: 'com.novoda.bintray-release'

    publish {
        artifactId = 'retropiler-runtime'
        desc = 'Runtime Library for Retropiler'

        def metadata = rootProject.ext.metadata
        userOrg = metadata.userOrg
        groupId = metadata.groupId
        publishVersion = metadata.version
        website = metadata.website
        repository = metadata.repository

        // The runtime library comes from AOSP, which comes from Open JDK,
        // which licensed as GPL v2.0 with "classpath exception".
        licences = ["GPL-2.0+CE"]
    }
}

